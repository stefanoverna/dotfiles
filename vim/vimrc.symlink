" neobundle

if has('vim_starting')
  set nocompatible
  set runtimepath+=~/.vim/bundle/neobundle.vim/
endif

call neobundle#begin(expand('~/.vim/bundle'))

NeoBundleFetch 'Shougo/neobundle.vim'

" 16 colors colorscheme, for a uniform terminal experience
NeoBundle 'noahfrederick/vim-noctu'

" syntaxes
NeoBundle 'kchmck/vim-coffee-script'
NeoBundle 'mtscout6/vim-cjsx'
NeoBundle 'slim-template/vim-slim'
NeoBundle 'othree/html5.vim'
NeoBundle 'pangloss/vim-javascript'
NeoBundle 'digitaltoad/vim-jade'
NeoBundle 'vim-ruby/vim-ruby'
NeoBundle 'plasticboy/vim-markdown'
NeoBundle 'mxw/vim-jsx'

" ruby stuff
NeoBundle 'stefanoverna/vim-plumber'
NeoBundle 'stefanoverna/vim-alternate'
NeoBundle 'jgdavey/vim-blockle'
NeoBundle 'stefanoverna/vim-reformat-call'

" adding end after if, do, def and several other keywords
NeoBundle 'tpope/vim-endwise'

" Git (used with airline)
NeoBundle 'tpope/vim-fugitive'
NeoBundle 'airblade/vim-gitgutter'

" snippets
NeoBundle "SirVer/ultisnips"
NeoBundle "stefanoverna/snipmate-snippets"

" alignment
NeoBundle 'junegunn/vim-easy-align'

" nice status line
NeoBundle 'bling/vim-airline'
" open files in specific line (eg. vim ~/foo.rb:12)
NeoBundle 'bogado/file-line'
" handy tmux-vim panes navigation
NeoBundle 'christoomey/vim-tmux-navigator'
" visual tab hints
NeoBundle 'nathanaelkane/vim-indent-guides'
" smarter pasting
NeoBundle 'sickill/vim-pasta'
" %S
NeoBundle 'tpope/vim-abolish'
" Comment all the things
NeoBundle 'tpope/vim-commentary'
" cs"'
NeoBundle 'tpope/vim-surround'
" populate the argument list from the files in the quickfix list
NeoBundle 'henrik/vim-qargs'

" Project drawer
NeoBundle 'scrooloose/nerdtree'

" Rails I18n helpers
NeoBundle 'stefanoverna/vim-i18n'

" Fuzzy file search
NeoBundle 'wincent/Command-T'
NeoBundle 'rking/ag.vim'
NeoBundle 'haya14busa/incsearch.vim'

" dependencies
NeoBundle 'Shougo/vimproc', {
\ 'build' : {
\     'mac' : 'make -f make_mac.mak',
\    },
\ }
NeoBundle 'tmhedberg/matchit'
NeoBundle 'kana/vim-textobj-user'

call neobundle#end()
filetype plugin indent on

" If there are uninstalled bundles found on startup,
" this will conveniently prompt you to install them.
NeoBundleCheck

" The set nocompatible setting makes vim behave in a more useful way (the default) than the vi-compatible manner.
set nocompatible

" Enable syntax highlighting
syntax enable

" Tab command-line completion
set wildmenu
" Complete till longest common string, and open suggestions menu
set wildmode=longest:full
set wildignore+=*/.git/*,*/.hg/*,*/.svn/*,*/.sass-cache/*,*/tmp/*,*/node_modules/*

" Default charset encoding.
set encoding=utf-8

" Leader key
let mapleader=","

" Single line command status
set cmdheight=1

" My favourite syntax colorscheme (iterm 256 colors)
set t_Co=16
set background=dark
colorscheme noctu
hi VertSplit ctermbg=8 ctermfg=8
hi ColorColumn ctermbg=0 ctermfg=NONE

hi! link Define Function
hi! link Macro Function
hi! link Tag SpecialComment

" Line numbering
set number
" Show the cursor position all the time
set cursorline
" Silence bells
set noerrorbells

" Display incomplete commands
set showcmd

" Allow backgrounding buffers without writing them, and remember marks/undo
" for backgrounded buffers
set hidden

" == Whitespace ==
" Don't wrap lines
set nowrap
" A tab is two spaces
set tabstop=2
" An autoindent (with <<) is two spaces
set shiftwidth=2
" Use spaces, not tabs
set expandtab
" Show invisible characters
set list
" backspace through everything in insert mode
set backspace=indent,eol,start

" A tab should display as "▸", trailing whitespace as "·"
" precedes and extends are the characters to sho when wrap
" is off and the line continues beyond the right of the screen
set listchars=trail:·,precedes:«,extends:»,tab:→\ 

" == Searching ==
" Highlight matches
set hlsearch
" Incremental searching
set incsearch
" Searches are case insensitive...
set ignorecase
" ...unless they contain at least one capital letter
set smartcase
" clear the search buffer when hitting return (but preserve natural behaviour
" in quickfix windows)
nnoremap <expr> <CR> (&buftype is# "quickfix" ? "<cr>" : ":\<C-u>nohlsearch\n")

" Treat JSON files like JavaScript
au BufNewFile,BufRead *.json set ft=javascript

" Thorfile, Rakefile, Vagrantfile and Gemfile are Ruby
au BufRead,BufNewFile {Podfile,Gemfile,Rakefile,Vagrantfile,Thorfile,config.ru} set ft=ruby

" Cakefile is Yaml
au BufRead,BufNewFile {Cakefile} set ft=coffee

" Remember last location in file, but not for commit messages.
autocmd BufReadPost *
  \ if &filetype !~ '^git\c' && line("'\"") > 0 && line("'\"") <= line("$") |
  \   exe "normal g`\"" |
  \ endif

au Filetype gitcommit setlocal textwidth=72 colorcolumn=72

" Automatically remove trailing spaces on some files (and fix tabs)
au BufWritePre *.rb,*.css,*.sass,*.scss,*.coffee :%s/\s\+$//e | :retab

" Slim comments start with /
autocmd FileType slim set commentstring=\/\ %s

" provide some context when editing
set scrolloff=3

function <SID>create_and_save_directory()
  let s:directory = expand('<afile>:p:h')
  if !isdirectory(s:directory)
    call mkdir(s:directory, 'p')
  endif
endfunction

" Automatically create parent directory on save if directory doesn't exist
au BufWritePre,FileWritePre * call <SID>create_and_save_directory()

set backupdir=~/.dotfiles/vim/_backup    " where to put backup files.
set directory=~/.dotfiles/vim/_temp      " where to put swap files.
set backupskip=/tmp/*,/private/tmp/*

" makes work arrows in visual mode
noremap <Up> <nop>
noremap <Down> <nop>
noremap <Left> <nop>
noremap <Right> <nop>

" == Ruby blocks ==
runtime macros/matchit.vim

" leader-f for The Silver Searcher
nnoremap <leader>f :Ag '

" == Command-T ==
nnoremap <silent> <Leader>l :CommandT<CR>
let g:CommandTRefreshMap='<C-r>'
let g:CommandTMaxHeight=20
let g:CommandTMinHeight=5

" == DestroyAllSoftware tips ==

" Switch between the last two files
nnoremap <leader><leader> <c-^>

" Open routes.rb
noremap <leader>gr :topleft :split config/routes.rb<cr>

" Open database.yml
noremap <leader>gd :topleft :split config/database.yml<cr>

" Open schema.rb
noremap <leader>gs :topleft :split db/schema.rb<cr>

" Open Gemfile
noremap <leader>gg :topleft :split Gemfile<cr>

" Can't be bothered to understand the difference between ESC and <c-c> in
" insert mode
inoremap <c-c> <esc>

" == colorpick ==
" Needs https://github.com/jnordberg/color-pick
function! ColorPick()
  let word = expand("<cword>")
  let hex = system('colorpick -startColor ' . word)
  if word =~ '\([a-fA-F1-9]\{3,6\}\)'
    execute "normal! ciw" . hex
  else
    execute "normal! a#" . hex
  endif
endfunction
nnoremap <leader>c :call ColorPick()<CR>

" == Vertical line at 80 chars ==
set colorcolumn=78

" == Paste from clipboard ==
set clipboard=unnamed " this needs vim 7.3 with +clipboard

" == Current line highlighter ==
au WinLeave * set nocursorline nocursorcolumn
au WinEnter * set cursorline
set cursorline
hi CursorLine cterm=NONE ctermbg=0

" == %% gets converted to directory of current file ==
cnoremap %% <C-R>=expand('%:h').'/'<cr>

" == Prefills :edit command with the current dir ==
map <leader>e :edit %%

" == Mouse support
" Send more characters for redraws
set ttyfast
" Enable mouse use in all modes
set mouse=a
" Set this to the name of your terminal that supports mouse codes.
" Must be one of: xterm, xterm2, netterm, dec, jsbterm, pterm
set ttymouse=xterm2

" == Disable F1 opening help
nnoremap <F1> :echo<CR>
inoremap <F1> <C-o>:echo<CR>

" == Indent Guides
let g:indent_guides_auto_colors = 0
let g:indent_guides_enable_on_vim_startup = 1
autocmd VimEnter,Colorscheme * :hi IndentGuidesOdd  ctermbg=0
autocmd VimEnter,Colorscheme * :hi IndentGuidesEven ctermbg=NONE

" quick save and quit!
nnoremap s :w<CR>

" q closes buffer, Q records macros
nnoremap Q q
nnoremap q :q<CR>

" sessions
let g:session_directory = "."
let g:session_autoload = 'yes'
let g:session_autosave = 'yes'
set sessionoptions-=buffers

" no backup files
set nobackup
set nowritebackup
set noswapfile

" Move cursor by display lines when wrapping
nnoremap j gj
nnoremap k gk
vnoremap j gj
vnoremap k gk

" Move to beginning and end of line easily
nnoremap H ^
nnoremap L $

" Let's make escape better, together.
inoremap jk <Esc>

" split
nnoremap <leader>v :execute "vsplit " . bufname("#")<CR>
nnoremap <leader>x :execute "split " . bufname("#")<CR>

" y u no consistent?
nnoremap Y y$

let NERDTreeIgnore=['node_modules/', 'stubs/', 'log/', '.DS_Store']

" <leader>A runs all the specs
nnoremap <silent> <leader>A :<C-U>w \| call RunAllSpecs()<CR>

function! RunAllSpecs()
  let currentSyntax = &filetype
  if currentSyntax == 'coffee' || currentSyntax == 'javascript'
    call Plumber("mocha test/**/*.js")
  else
    call Plumber("rspec spec")
  endif
endfunction

" Convert hashrockets into new 1.9 hash syntax
noremap <leader>rr :%s/:\(\w\+\)\s*=>/\1:/ge<CR><C-o>

" Remove trailing spaces
noremap <leader>ss :%s/\s\+$//e<CR><C-o>

cmap w!! w !sudo tee %
command! KillControlM :normal :%s/<C-V><C-M>//e<CR><C-O><CR>

" More natural split opening
set splitbelow
set splitright

" Airline
" Always show the status bar
set laststatus=2
let g:airline_left_sep = ''
let g:airline_right_sep = ''
let g:airline_enable_syntastic = 0
let g:airline_enable_bufferline = 0
let g:airline_enable_branch = 1

let g:airline_linecolumn_prefix = '␊ '
let g:airline_linecolumn_prefix = '␤ '
let g:airline_linecolumn_prefix = '¶ '
let g:airline_branch_prefix = '⎇  '
let g:airline_paste_symbol = 'ρ'
let g:airline_theme='badwolf'

" Plumber
let g:plumber_precommand = 'plumber-exit-copy-mode'

" Git gutter
let g:gitgutter_realtime = 0
let g:gitgutter_eager = 0

" autocomplete keyword with \
inoremap <expr> \ '<C-n>'

" Cursor shapes
if exists("$TMUX")
  let &t_SI = "\<Esc>[3 q"
  let &t_EI = "\<Esc>[0 q"
else
  let &t_SI = "\<Esc>]50;CursorShape=1\x7"
  let &t_EI = "\<Esc>]50;CursorShape=0\x7"
endif

" Insert only one space when joining lines that contain sentence-terminating
" punctuation like `.`.
set nojoinspaces

" Saves current file and reloads current Chrome tab
map <leader>z :w\|:silent !reload-chrome<cr>\|:redraw!<cr>

" Promote variable to Rspec let
function! PromoteToLet()
  normal! dd
  " :exec '?^\s*it\>'
  normal! P
  .s/\(\w\+\) = \(.*\)$/let(:\1) { \2 }/
  normal ==
endfunction
command! PromoteToLet :call PromoteToLet()
map <leader>p :PromoteToLet<cr>

" Highlight VCS conflict markers
match ErrorMsg '^\(<\|=\|>\)\{7\}\([^=].\+\)\?$'

" select last paste in visual mode
nnoremap <expr> vv '`[' . strpart(getregtype(), 0, 1) . '`]'

" reformat method call/function call/hash
map <leader>u :ReformatCall<cr>

" I18n
vmap <Leader>z :call I18nTranslateString()<CR>

" Test quickfix list management: If the tests write a tmp/quickfix file, these
" mappings will navigate through it

function! GetBufferList()
  redir =>buflist
  silent! ls
  redir END
  return buflist
endfunction

function! BufferIsOpen(bufname)
  let buflist = GetBufferList()
  for bufnum in map(filter(split(buflist, '\n'), 'v:val =~ "'.a:bufname.'"'), 'str2nr(matchstr(v:val, "\\d\\+"))')
    if bufwinnr(bufnum) != -1
      return 1
    endif
  endfor
  return 0
endfunction

function! ToggleQuickfix()
  if BufferIsOpen("Quickfix List")
    cclose
  else
    call OpenQuickfix()
  endif
endfunction

function! OpenQuickfix()
  cgetfile tmp/quickfix
  topleft cwindow
  if &ft == "qf"
      cc
  endif
endfunction

nnoremap <leader>q :call ToggleQuickfix()<cr>
nnoremap <leader>Q :cc<cr>
nnoremap <leader>j :cnext<cr>
nnoremap <leader>k :cprev<cr>

" disable folding
set foldlevelstart=20

" snippets
let g:UltiSnipsExpandTrigger="<tab>"
let g:UltiSnipsJumpForwardTrigger="<tab>"
let g:UltiSnipsJumpBackwardTrigger="<s-tab>"

" Close VIM if the only window left open is a NERDTree
au bufenter * if (winnr("$") == 1 && exists("b:NERDTreeType") && b:NERDTreeType == "primary") | q | endif

" NERDtree
"
nnoremap <leader>d :NERDTreeToggle<CR>
let NERDTreeHijackNetrw=1
let NERDTreeShowHidden=1

function! CustomTreeToggle()
  if !exists("t:NERDTreeBufName") || bufwinnr(t:NERDTreeBufName) == -1
    if strlen(expand("%:p")) > 0
      exec ':NERDTreeFind'
    else
      exec ':NERDTree'
    end
  else
    exec ':NERDTreeClose'
  endif
endfunction
noremap <leader>d :call CustomTreeToggle()<cr>

" Improved incremental search

let g:incsearch#magic = '\v'

map /  <Plug>(incsearch-forward)
map ?  <Plug>(incsearch-backward)
map g/ <Plug>(incsearch-stay)

" Start interactive EasyAlign in visual mode (e.g. vip<Enter>)
vmap <Enter> <Plug>(EasyAlign)

" Start interactive EasyAlign for a motion/text object (e.g. gaip)
nmap ga <Plug>(EasyAlign)

" RJS Syntax also on .js files
let g:jsx_ext_required = 0

function! RemoveTrailingNewLines()
  " \%$ -> end of file
  %s/[\r\n]*\%$//
endfunction

" POSIX says a line is a sequence of zero or more non-newline characters plus
" a terminating newline character
autocmd BufWritePre * :call RemoveTrailingNewLines()

